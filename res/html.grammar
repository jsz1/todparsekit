/*
    An HTML grammar for use with TDParseKit

    this grammar is intentionally very forgiving (non-strict)
    the point here is to highlight, not validate, HTML.
*/

@symbols = '<!DOCTYPE' '<?' '?>';
@multiLineComments = '<!--' '-->';

@start          = any*;
any             = element | text | doctype | pi;

pi              = '<?' tagName attr* '?>';

doctype         = '<!DOCTYPE' /[^>]+/* gt;
element         = emptyTag | startTag elementContent* endTag;
elementContent  = element | text;
text            = /[^<]+/;

emptyTag        = lt tagName attr* fwdSlash gt;
startTag        = lt junk? tagName attr* gt;
endTag          = lt fwdSlash tagName gt;

tagName         = Word;

attr            = attrName (eq attrValue)?;
attrName        = Word;
attrValue       = Word | QuotedString;

junk            = Symbol & !'>' & !'/';

eq              = '=';
lt              = '<';
gt              = '>';
fwdSlash        = '/';

comment         = Comment;
