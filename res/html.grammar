/**
    An HTML grammar for use with TDParseKit

    this grammar is intentionally very forgiving (non-strict)
    the point here is to highlight, not validate, HTML.
*/

@multiLineComments = '<!--' '-->';
@symbols = '</';

@start          = elements;
elements        = (startTag elementContent endTag)+;
elementContent  = (elementChildren);
elementChildren = (text | elements | comment)*;

text            = (word | num)+;
word            = Word;
num             = Num;

startTag        = lt elementName attribute* gt;
elementName     = Word;
endTag          = endTagStart elementName gt;

lt              = '<';
gt              = '>';
fwdSlash        = '/';
endTagStart     = '</';

comment         = Comment;

attribute       = attrName eq? attrValue?;
attrName        = Word;
eq              = '=';
attrValue       = Word | QuotedString;