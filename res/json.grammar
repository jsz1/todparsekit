/**
    A Grammar for JSON.
    
    Feed this to -[TDGrammarParserFactory parserForGrammar:assembler:] along with a reference
    to an assembler (a call back delegate), and it will return a TDParser object which can 
    parse strings conforming to this language/grammar.
    
    The when the returned parser is used, the provided assembler will receive method callbacks like:

    -workOnOpenCurlyAssembly:
    -workOnObjectAssembly:
    -workOnPropertyAssembly:
    -workOnArrayAssembly:
    etc.
    
    whenever a grammar production of the corresponding name has been matched.
    
    Each callback method must accept a single TDAssembly argument. The provided assembly
    will contain information about the progress made on the the string currently being parsed.
    Also the assembly's stack will contain the tokens parsed so far in the current statement.
 */

@start        = Empty | array | object;

openCurly     = '{';
closeCurly    = '}';
openBracket   = '[';
closeBracket  = ']';
comma         = ',';
colon         = ':';

object        = openCurly objectContent closeCurly;
objectContent = Empty | actualObject;
actualObject  = property commaProperty*;
commaProperty = comma property;
propertyName  = QuotedString;
property      = propertyName colon value;         // NOTE: property names are quoted in JSON

array         = openBracket arrayContent closeBracket;
arrayContent  = Empty | actualArray;
actualArray   = value commaValue*;
commaValue    = comma value;

value         = null | true | false | array | object | number | string;
string        = QuotedString;
number        = Num;
null          = 'null';
true          = 'true';
false         = 'false';
